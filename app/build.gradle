apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"
    defaultConfig {
        applicationId "com.gmail.fattazzo.formula1world"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 26
        versionName "2.6"
        setProperty("archivesBaseName", "Total-GP-world-$versionName")

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            applicationVariants.all { variant ->
                variant.outputs.each { output ->
                    def newName = output.outputFile.name
                    newName = newName.replace("-release", "")
                    //noinspection GroovyAssignabilityCheck
                    output.outputFile = new File(output.outputFile.parent, newName)
                }
            }
        }
    }
}

afterEvaluate {
    android.sourceSets.all { sourceSet ->
        if (!sourceSet.name.startsWith('test') || !sourceSet.name.startsWith('androidTest')) {
            sourceSet.kotlin.setSrcDirs([])
        }
    }
}


// Align test resources folder ------------------------------------------
task cleanTestResources(type: Delete) {
    delete 'src/test/resources/assets'
}

task copyAssetsTestFiles(type: Copy, dependsOn: 'cleanTestResources') {
    from 'src/main/assets'
    into 'src/test/resources/assets'
}


preBuild.dependsOn copyAssetsTestFiles
preBuild.mustRunAfter copyAssetsTestFiles

clean.dependsOn copyAssetsTestFiles
clean.mustRunAfter copyAssetsTestFiles
// -----------------------------------------------------------------------

kapt {
    correctErrorTypes = true
}

def AAVersion = "4.3.1"

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    kapt "org.androidannotations:androidannotations:$AAVersion"
    compile "org.androidannotations:androidannotations-api:$AAVersion"

    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'

    compile 'com.google.code.gson:gson:2.7'

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.0'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'

    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'

    compile 'com.android.support.constraint:constraint-layout:1.0.2'

    compile 'com.ToxicBakery.viewpager.transforms:view-pager-transforms:1.2.32@aar'

    compile 'org.jsoup:jsoup:1.10.3'

    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'

    compile 'com.yarolegovich:lovely-dialog:1.0.7'

    compile 'com.appyvet:materialrangebar:1.3'

    //------------------ TEST --------------------------------
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.5"
    testCompile("com.android.support.test:runner:0.5") {
        exclude group: 'com.android.support', module: 'support-annotations'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // Espresso-contrib for DatePicker, RecyclerView, Drawer actions, Accessibility checks, CountingIdlingResource
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude module: 'support-annotations'
        exclude module: 'support-v4'
        exclude module: 'support-v13'
        exclude module: 'recyclerview-v7'
        exclude module: 'design'
    }

    androidTestCompile('com.android.support.test.espresso:espresso-web:2.2.2') {
        exclude module: 'support-annotations'
    }

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
}
repositories {
    mavenCentral()

    // ActiveAndroid
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}
